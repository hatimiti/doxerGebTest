apply plugin: 'groovy'
apply plugin: 'eclipse'

ext {
	// The drivers we want to use
	// InternetExplorerドライバはWindows8.1 and ver11 の環境でErrorとなったため現在コメント化
	// WebDriver側では対応が難しいみたい。レジストリをいじる方法を試したがダメだった。
	drivers = ["firefox", "chrome", /*"ie"*/]

	ext {
		groovyVersion = '2.3.6'
		gebVersion = '0.9.3'
		seleniumVersion = '2.43.1'
		chromeDriverVersion = '2.10'
		phantomJsVersion = '1.9.7'
	}
}

version = '1.0'

repositories {
	mavenCentral()
}

dependencies {
	// If using Spock, need to depend on geb-spock
	testCompile "org.gebish:geb-spock:$gebVersion"
	testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
		exclude group: "org.codehaus.groovy"
	}
	testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"

	// If using JUnit, need to depend on geb-junit (3 or 4)
	testCompile "org.gebish:geb-junit4:$gebVersion"
	testCompile "net.sourceforge.htmlunit:htmlunit:2.15"

	// Drivers
	testCompile "org.seleniumhq.selenium:selenium-htmlunit-driver:$seleniumVersion"
	testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
	testCompile "org.seleniumhq.selenium:selenium-ie-driver:$seleniumVersion"
	testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
}

drivers.each { driver ->
	task "${driver}Test"(type: Test) {
		reports {
			html.destination = reporting.file("$name/tests")
			junitXml.destination = file("$buildDir/test-results/$name")
		}

		outputs.upToDateWhen { false }  // Always run tests

		systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
		systemProperty "geb.env", driver

		// If you wanted to set the baseUrl in your build…
		// systemProperty "geb.build.baseUrl", "http://myapp.com"
	}
}

test {
	dependsOn drivers.collect { tasks["${it}Test"] }
	enabled = false
}
